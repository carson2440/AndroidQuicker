apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.SDK_VERSION
    defaultConfig {
        applicationId "com.carson.androidquicker"
        minSdkVersion MIN_VERSION
        targetSdkVersion rootProject.ext.SDK_VERSION
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //svg 兼容性设置，需要添加appcompat-v7,Activity需要使用AppCompatActivity.
        //在application中设置AppCompatDelegate.setCompatVectorFromResourcesEnabled(true);
        vectorDrawables.useSupportLibrary = true
    }

    //开启mvvn 数据双向绑定
    dataBinding {
        enabled = true
    }
    //代码风格兼容Java1.8版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //定制输出APK的名字
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "quicker-${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
    //指定APP的签名信息
    signingConfigs {
        release {
//            storeFile file('E://Android/leng360.keystore')
//            storePassword "leng360"
//            keyAlias "leng360.keystore"
//            keyPassword "leng360"
        }
    }
    //定制正式环境和测试环境的参数输出
    buildTypes {
        debug {
            buildConfigField "String", "API_HOST", "\"https://api.leng360localtest.com/v1000\""
            buildConfigField "String", "SOC_HOST", "\"wss://platform.leng360localtest.com:9502\""
            minifyEnabled false
            manifestPlaceholders = [APP_NAME: "@string/app_name_debug"]
//            signingConfig signingConfigs.release
        }
        release {
            buildConfigField "String", "API_HOST", "\"https://api.leng360.com/v1000\""
            buildConfigField "String", "SOC_HOST", "\"wss://platform.leng360.com:9502\""
            minifyEnabled false
            manifestPlaceholders = [APP_NAME: "@string/app_name_release"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            signingConfig signingConfigs.release
        }
    }

}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
//    implementation "com.android.support:appcompat-v7:${SUPPORT_VERSION}"
    implementation getRootProject().ext.dependencies.appcompat_v7
    implementation getRootProject().ext.dependencies.design
    implementation "com.android.support:recyclerview-v7:${SUPPORT_VERSION}"
    api project(':quicker')

    implementation "android.arch.lifecycle:extensions:${ARCH_VERSION}"
    implementation "android.arch.lifecycle:common-java8:${ARCH_VERSION}"
    annotationProcessor "android.arch.lifecycle:compiler:${ARCH_VERSION}"

    implementation 'com.ashokvarma.android:bottom-navigation-bar:2.0.4'
    //glide 高版本依賴的库和项目有冲突，不兼容
    implementation 'com.github.bumptech.glide:glide:3.7.0'

}
